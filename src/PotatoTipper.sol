// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.28;

// modules
import {PotatoTipperConfig} from "./PotatoTipperConfig.sol";

// interfaces
import {IERC725Y} from "@erc725/smart-contracts/contracts/interfaces/IERC725Y.sol";
import {IERC165} from "@openzeppelin/contracts/interfaces/IERC165.sol";
import {
    ILSP1UniversalReceiverDelegate as ILSP1Delegate
} from "@lukso/lsp1-contracts/contracts/ILSP1UniversalReceiverDelegate.sol";

// libraries
import {ERC165Checker} from "@openzeppelin/contracts/utils/introspection/ERC165Checker.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import "./PotatoTipperSettingsLib.sol" as SettingsLib;

// constants
import {_INTERFACEID_LSP0} from "@lukso/lsp0-contracts/contracts/LSP0Constants.sol";
import {_TYPEID_LSP26_FOLLOW, _TYPEID_LSP26_UNFOLLOW} from "@lukso/lsp26-contracts/contracts/LSP26Constants.sol";
import {_FOLLOWER_REGISTRY, _POTATO_TOKEN} from "./Constants.sol";

// events
import {PotatoTipSent, PotatoTipFailed} from "./Events.sol";

// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚£Ä‚°§‚£î‚¢≤‚°í‚¢¶‚°ô‚°¥‚£í‚£ñ‚°†‚£Ñ‚£Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚°¥‚°û‚°π‚¢Ü‚£ù‚£§‚££‚°ô‚¢¶‚£ô‚°¥‚°°‚¢¶‚°ô‚£±‚†∫‚£≠‚£ñ‚†§‚°Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚£¥‚†ª‚£°‚¢≥‚†µ‚†õ‚†â‚†Ä‚†Ä‚†Ä‚°Ä‚¢Ä‚†Ä‚°à‚†ô‚¢¢‚°ù‚°§‚¢ì‚†¶‚°ú‚†ª‚£ú‚°¢‚£Ñ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚°¥‚£õ‚†¨‚££‚†ã‚†Å‚¢Ä‚††‚†ê‚†à‚°Ä‚¢Å‚†Ä‚†Ç‚††‚†ê‚†Ä‚†Ñ‚°ø‚£ê‚°ü‚†â‚†â‚†≥‚£å‚†≥‚£ú‚¢¢‚°Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢†‚°û‚°∏‚£§‚†ü‚†Ä‚†Ä‚†å‚¢Ä‚†Ä‚†Ç‚†ê‚†Ä‚†Ñ‚†à‚†Ñ‚¢Å‚£Ñ‚°¨‚¢û‚°±‚£°‚¢õ‚£§‚£ê‚£Ä‚£º‚†≥‚°å‚¢ß‚°±‚°Ñ‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢†‚°≥‚¢ç‚°∂‚†Å‚†Ä‚†Ñ‚†°‚¢Ä‚£¢‚†¨‚°¥‚¢ì‚°û‚¢≤‚†´‚°ù‚¢≠‚†¢‚°ù‚¢¢‚°ì‚†¥‚£É‚¢Ü‚°£‚°ç‚¢¶‚†ì‚°º‚¢°‚†≥‚£∏‚°Ñ‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚†∑‚£©‚†û‚†Ä‚††‚¢Å‚°¥‚°∫‚¢ç‚°≤‚£ë‚†é‚°µ‚°®‚¢á‚¢≥‚†∏‚£°‚†ì‚£ç‚¢∑‚£Æ‚¢ì‚°ú‚£¢‚¢µ‚°™‚£•‚†õ‚£î‚°ã‚£∑‚°á‚£∑‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚¢æ‚†£‚°è‚°Ä‚†Ñ‚£°‚°è‚¢ñ‚°©‚¢ñ‚°±‚¢ú‚¢™‚†±‚£±‚¢ä‚†ß‚£ô‚°î‚¢´‚°î‚¢´‚°±‚¢é‚†¥‚£É‚†æ‚£Ω‚£∂‚£ã‚¢¶‚°π‚¢ø‚°õ‚£ß‚°á‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚¢Æ‚†£‚£ù‚†≥‚°¥‚°ö‚¢ß‚£ò‚¢£‚†ú‚¢¶‚°ô‚°¨‚¢é‚†µ‚£Ç‚¢è‚†≤‚£Ö‚†∫‚£°‚¢é‚¢£‚°ú‚£ä‚†∂‚°ë‚£é‚¢π‚¢∫‚£ª‚£Æ‚°ù‚¢¶‚°ô‚£∑‚£ª‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚°∞‚¢è‚†é‚£ï‚¢™‚£±‚¢£‚°ô‚¢Ü‚†Æ‚°ú‚¢™‚°±‚¢ú‚¢¢‚£ù‚¢¢‚°ç‚¢é‚°ï‚°™‚¢ï‚°≤‚¢å‚°£‚¢ú‚¢¢‚¢á‚°π‚¢§‚¢£‚†ì‚£é‚£õ‚†ø‚¢¶‚¢π‚£∑‚°π‚°Ñ
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£Ä‚°¥‚¢´‚°ô‚£¨‚†ö‚£å‚†¶‚°π‚¢ü‚£ª‚°ø‚¢∂‚£ç‚¢£‚°ú‚¢™‚°±‚¢è‚°£‚°ú‚¢é‚°¥‚°ô‚¢¶‚†±‚¢é‚°±‚°©‚¢ñ‚¢™‚°ë‚£é‚†≤‚£ç‚†≤‚°å‚¢û‚¢¢‚£ª‚¢û‚°µ‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚°æ‚¢£‚¢ç‚°£‚°ú‚†¥‚°ô‚¢Ü‚°≥‚¢°‚†è‚°¥‚¢©‚£ã‚†ú‚£Ü‚°ö‚¢•‚¢ö‚°¥‚°ë‚¢Æ‚°ë‚¢¶‚°ô‚¢Ü‚†Ø‚£ò‚†≤‚£Ö‚†´‚¢Ü‚†≥‚£å‚†≥‚£∏‚£∑‚£è‚¢é‚°±‚£Ø‚°ª‚£ú‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚£æ‚¢ü‚°¥‚£ã‚†¶‚°±‚¢é‚¢£‚†ù‚°∏‚°î‚¢´‚¢ú‚°∏‚¢Ö‚°é‚†û‚£§‚†π‚£ò‚†¶‚£í‚†≠‚°í‚£ç‚†¶‚£ô‚†é‚£ú‚£°‚†≥‚£å‚†≥‚£â‚†≥‚£å‚†≥‚£©‚¢õ‚†ª‚°å‚£æ‚°≥‚£ù‚¢ß‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚°¥‚¢ü‚°π‚£ª‚†ø‚°é‚¢ñ‚°±‚°©‚¢é‚£ö‚¢±‚£Æ‚†á‚£é‚†ñ‚£©‚†ú‚£±‚¢ä‚†µ‚°°‚¢û‚°∞‚¢£‚°ô‚£§‚¢ã‚¢¶‚°ô‚¢Ü‚°ñ‚°±‚£ä‚†µ‚£â‚†∂‚£°‚†ì‚°•‚¢é‚¢≥‚¢∏‚£∑‚¢´‚°Ω‚£∫‚†Ö
// ‚†Ä‚†Ä‚¢Ä‚¢Æ‚°ô‚£Ü‚¢£‚†µ‚°©‚¢ú‚†£‚£ú‚£°‚†≥‚£å‚†£‚£ç‚°ö‚°§‚¢õ‚°§‚¢õ‚¢§‚°ã‚°º‚°ë‚£é‚†±‚¢£‚°±‚¢Ü‚¢≠‚†¢‚°ù‚†≤‚¢¨‚°±‚¢ú‚°∏‚¢å‚†∂‚£°‚¢ã‚¢ñ‚°©‚¢é‚°ø‚£é‚¢∑‚°π‚£Ω‚†Ä
// ‚†Ä‚†Ä‚£º‚¢ç‚†ñ‚£±‚¢ä‚°ñ‚°ç‚£é‚†≥‚°∞‚¢Ü‚†≥‚£å‚¢ì‚†¶‚£±‚†©‚¢ñ‚°°‚¢è‚†¶‚£±‚¢°‚†≥‚°å‚°≠‚¢£‚¢ú‚°ä‚°ñ‚†≠‚°ú‚£ô‚†¶‚°±‚¢é‚°ú‚£ä‚†∂‚°°‚†û‚£å‚†ñ‚£ø‚£ù‚£Æ‚¢≥‚¢Ø‚°ç‚†Ä
// ‚†Ä‚¢∏‚£ª‚¢ú‚¢™‚°ë‚°é‚°¥‚¢ì‚°å‚¢á‚°ì‚¢é‚†≥‚£å‚°ö‚°ú‚†¥‚£ô‚¢¨‚°ö‚¢¨‚†≤‚£Ö‚¢é‚†≥‚¢¨‚£ë‚†£‚£é‚†ú‚°ú‚°•‚°ô‚¢Ü‚£ß‚°ì‚°º‚£ê‚¢£‚†é‚°µ‚¢©‚¢Ü‚£ø‚°ª‚£º‚£é‚£ü‚£û‚†É‚†Ä
// ‚†Ä‚£ü‚£ø‚°ò‚£Ü‚¢£‚°ï‚¢é‚°±‚¢™‚°ë‚¢Æ‚†©‚°ñ‚£°‚†û‚£å‚†≥‚°ú‚£∂‚£Ω‚£¶‚£ì‚¢¨‚¢ä‚°ù‚¢¢‚†é‚°µ‚°ò‚¢é‚°±‚°ú‚£©‚¢é‚¢ª‚†±‚°í‚°ç‚¢¶‚¢ã‚°¥‚¢ã‚£º‚£ó‚£ª‚£ø‚£ø‚°û‚°º‚†Ä‚†Ä
// ‚¢∏‚£Ω‚¢æ‚°±‚°å‚†∂‚°ò‚¢é‚°±‚¢£‚°ô‚¢Ü‚°è‚†¥‚£É‚†û‚£å‚†≥‚£ò‚°å‚¢≥‚†Ω‚£ª‚¢æ‚£Æ‚¢ú‚°°‚¢è‚°¥‚°ô‚£¨‚†±‚°ú‚°î‚°™‚¢•‚¢ã‚°ï‚¢Æ‚°ë‚†Æ‚£î‚°ø‚£≥‚¢é‚°∑‚£π‚¢∂‚£π‚†É‚†Ä‚†Ä
// ‚¢∏‚£û‚¢ß‚£∑‚¢â‚°û‚°©‚¢Æ‚£µ‚°£‚¢é‚¢£‚°ú‚†≥‚°å‚†û‚£å‚¢£‚†ï‚£ä‚¢á‚†Æ‚£ë‚¢´‚°ô‚¢¶‚°ô‚¢Ü‚°ñ‚°ç‚£Ü‚†≥‚°ú‚°∏‚£ë‚¢é‚°±‚¢ä‚¢¶‚°ô‚£º‚¢û‚°≥‚£ù‚¢æ‚°±‚£è‚°û‚°è‚†Ä‚†Ä‚†Ä
// ‚†∏‚£æ‚£è‚†æ‚£ß‚£ò‚†±‚¢´‚°ô‚£•‚¢ã‚¢ñ‚£ò‚¢£‚†≠‚£ô‚¢§‚°ã‚°º‚¢°‚¢é‚†≥‚£å‚¢£‚°ú‚¢¶‚°ô‚°≤‚†∏‚°î‚¢£‚°ì‚£ú‚£±‚£¨‚†í‚°≠‚£©‚¢Ü‚£Ω‚¢≥‚¢Ø‚°ù‚£Æ‚¢≥‚°ù‚£Æ‚°ù‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚£∑‚¢´‚°ü‚°Ω‚£Ü‚¢è‚†•‚£ì‚¢§‚¢ã‚°ñ‚°∏‚°å‚†∂‚£â‚¢¶‚†±‚£±‚°â‚¢Æ‚¢±‚°ò‚°Ü‚†û‚£§‚†ì‚£ç‚¢£‚†ù‚£¢‚†ï‚°∫‚¢Ω‚†ª‚£ç‚¢≤‚£±‚†æ‚£≠‚£õ‚¢Æ‚£ù‚¢Æ‚°≥‚°Ω‚°û‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚¢∏‚£ª‚£ú‚°≥‚£ù‚°ª‚£î‚¢£‚†é‚£ñ‚†∏‚£±‚¢ò‚°£‚¢ï‚°™‚†ï‚¢¶‚°ô‚¢Ü‚°á‚¢û‚°∏‚£â‚¢¶‚†π‚°å‚¢é‚¢é‚°±‚¢é‚°±‚¢é‚°±‚°º‚°æ‚£≠‚£õ‚¢∂‚°π‚£û‚°º‚££‚¢ü‚°ù‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚¢∑‚£´‚†∑‚£≠‚¢≥‚£è‚¢∑‚¢æ‚£à‚°ì‚†¶‚£ç‚°í‚†ß‚°ú‚£ô‚†¶‚°ô‚¢¶‚£ø‚°¶‚¢±‚¢ä‚¢¶‚¢ã‚°º‚£â‚†¶‚°ì‚£¨‚£±‚¢æ‚°π‚£è‚¢∑‚££‚¢ü‚£Æ‚¢≥‚°ù‚£æ‚£±‚†è‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†à‚¢Ø‚£ü‚°º‚£≥‚¢é‚°ü‚£Æ‚¢Ø‚°Ω‚£≥‚¢¶‚£ô‚°ú‚°ú‚°¢‚¢ù‚°ò‚¢¶‚°ô‚°¥‚¢ã‚°ú‚£¢‚¢ç‚¢≤‚£°‚†æ‚£µ‚¢´‚°û‚£ß‚¢ª‚°º‚£ø‚£ø‚°æ‚£ú‚¢ß‚°ª‚£∂‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†à‚¢ø‚¢æ‚°µ‚£õ‚†æ‚£µ‚£ø‚£æ‚£≠‚¢Ø‚°ù‚£æ‚£π‚¢≥‚°ü‚£û‚¢¶‚°≥‚£ú‚°≥‚£û‚¢∂‚£´‚¢ü‚°º‚£ª‚£º‚£≥‚¢ª‚£º‚££‚†ø‚£Ω‚£õ‚¢∑‚°π‚£Æ‚†ü‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†ª‚£Ω‚£Ø‚£ü‚£ø‚£ø‚°ø‚£è‚¢æ‚°π‚¢∂‚£≠‚¢≥‚°ù‚£Æ‚¢≥‚°ù‚£ß‚¢ª‚°ú‚£ß‚£õ‚¢Æ‚£≥‚¢≥‚¢æ‚£ª‚¢ü‚£æ‚£Ω‚£õ‚°∂‚£π‚¢Æ‚†ø‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†ª‚£ø‚£∑‚£π‚¢û‚°Ω‚¢Æ‚£ù‚°≥‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£é‚¢∑‚£ø‚£ß‚£ü‚¢Æ‚£≥‚£õ‚°æ‚£ù‚°ª‚£û‚£Ω‚¢ø‚°Ω‚†ü‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†≥‚¢ª‚°ø‚£º‚°≥‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£æ‚£ø‚£ø‚¢ø‚£´‚£ø‚£ø‚°ú‚£ß‚¢ü‚°æ‚†ú‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†â‚†ì‚†ø‚£π‚°û‚°µ‚¢Ø‚°û‚£µ‚£´‚£û‚£µ‚£≥‚°û‚£º‚¢£‚°∑‚£ª‚°º‚†Ω‚†ö‚†â‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†â‚†ë‚†õ‚†í‚†õ‚†ö‚†ì‚†ì‚†õ‚†ä‚†â‚†â‚†Ä‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
using {SettingsLib.loadTipSettingsRaw} for IERC725Y;
using {SettingsLib.decodeTipSettings} for bytes;
using {ERC165Checker.supportsInterface} for address;

///
/// @title Contract allowing a üÜô to react when receiving a new follower, by tipping ü•î $POTATO tokens to
/// this new follower. Can be used as an automated incentive mechanism.
///
/// @author Jean Cavallera (CJ42)
///
/// @dev Terminology:
/// - BPT = "Before Potato Tipper" = for followers that already followed a user before it connected the Potato Tipper
/// - APT = "After Potato Tipper" = for followers that followed a user after it connected the Potato Tipper"
///
/// @notice ‚ö†Ô∏è Disclaimer: this contract has not been formally audited by an external third party
/// auditor. The contract does not guarantee to be bug free. Use responsibly at your own risk.
///
contract PotatoTipper is IERC165, ILSP1Delegate, PotatoTipperConfig {
    using Strings for address;

    /// @dev Track `follower` addresses that already received a tip from a `user`
    mapping(address follower => mapping(address user => bool)) internal _tippedFollowers;

    /// @dev Track followers that followed a user since it connected the Potato Tipper.
    mapping(address follower => mapping(address user => bool)) internal _postInstallFollowers;

    /// @dev Track existing followers that were already following a user before it connected the Potato Tipper.
    /// Tracked via `_handleOnUnfollow(...)` to mark them as ineligible for a tip if they re-follow.
    mapping(address follower => mapping(address user => bool)) internal _existingFollowersUnfollowedPostInstall;

    /// Read functions
    /// ---------------

    /// @notice This contract only supports the LSP1 Universal Receiver Delegate and ERC165 interfaces.
    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return interfaceId == type(ILSP1Delegate).interfaceId || interfaceId == type(IERC165).interfaceId;
    }

    /// @notice Check if a `follower` has already been tipped by a `user`.
    function hasReceivedTip(address follower, address user) external view returns (bool) {
        return _tippedFollowers[follower][user];
    }

    /// @notice Returns if `follower` has followed `user` since it connected its üÜô to the Potato Tipper contract.
    function hasFollowedPostInstall(address follower, address user) external view returns (bool) {
        return _postInstallFollowers[follower][user];
    }

    /// @notice Returns true if `follower` was already a follower of `user` before it connected the Potato Tipper and
    /// later unfollowed. Defines existing followers that are not eligible for a tip.
    function hasExistingFollowerUnfollowedPostInstall(address follower, address user) external view returns (bool) {
        return _existingFollowersUnfollowedPostInstall[follower][user];
    }

    /// Handler functions
    /// ---------------

    /// @notice Handle follow/unfollow notifications + automatically tip ü•î tokens to new follower.
    ///
    /// @dev Called by the `universalReceiver(...)` function from a user's üÜô.
    ///
    /// LSP26 notification calls don't revert, but calls from a üÜô to this function could revert.
    /// Avoid reverting so the internal call stack trace stays clear of revert errors, and return a `messageStatus`
    /// that can be decoded from the `returnedData` parameter of the `UniversalReceiver` event on the user's üÜô.
    ///
    /// @param sender The address that notified the user's UP (MUST be the LSP26 Follower Registry)
    /// @param typeId MUST be a follow or unfollow notification type ID
    /// @param data MUST be the follower address sent by the LSP26 Follower registry when notifying
    /// @return messageStatus A user-friendly message describing how the call was handled
    function universalReceiverDelegate(
        address sender,
        uint256, // value (unused parameter)
        bytes32 typeId,
        bytes calldata data
    )
        external
        returns (bytes memory messageStatus)
    {
        if (sender != address(_FOLLOWER_REGISTRY)) return unicode"‚õìÔ∏è‚Äçüí•‚ùå Not called by Follower Registry";
        if (data.length != 20) return unicode"‚õìÔ∏è‚Äçüí•‚ùå Invalid data received. Must be a 20 bytes address";

        // Only üÜô‚úÖ allowed to receive tips, üîë‚ùå not EOAs
        address follower = address(bytes20(data));
        bool isErc725Account = follower.supportsInterface(_INTERFACEID_LSP0);
        if (!isErc725Account) return unicode"‚õìÔ∏è‚Äçüí•‚ùå Only üÜô allowed to receive tips";

        if (typeId == _TYPEID_LSP26_FOLLOW) return _handleOnFollow(follower);
        if (typeId == _TYPEID_LSP26_UNFOLLOW) return _handleOnUnfollow(follower);

        return unicode"‚õìÔ∏è‚Äçüí•‚ùå Not a follow/unfollow notification";
    }

    /// @notice Handle a new follower notification and tip ü•î $POTATO tokens if the follower is eligible.
    ///
    /// @dev Before attempting to transfer a tip, re-validate against the LSP26 Follower Registry to ensure
    /// this function is running in the context of a legitimate follow notification and not from a spoofed call.
    /// Also rejects re-follow attempts from pre-existing followers.
    ///
    /// @return message A human-readable message returned to indicate successful tip, or an error reason.
    function _handleOnFollow(address follower) internal returns (bytes memory message) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(follower, msg.sender);
        if (!isFollowing) return unicode"‚õìÔ∏è‚Äçüí•‚ùå Not a legitimate follow";

        // Prevent double tipping a follower if unfollow -> re-follow ü•î üöú
        bool isAlreadyTipped = _tippedFollowers[follower][msg.sender];
        if (isAlreadyTipped) return unicode"üîç‚ùå Follower already tipped";

        // Existing followers are not eligible. A user does not gain any benefit from tipping them if they re-follow
        bool isExistingFollower = _existingFollowersUnfollowedPostInstall[follower][msg.sender];
        if (isExistingFollower) return unicode"üîç‚ùå Existing followers not eligible to receive tips";

        bool isNewFollowerRegisted = _postInstallFollowers[follower][msg.sender];
        if (!isNewFollowerRegisted) _postInstallFollowers[follower][msg.sender] = true;

        bytes memory settingsValue = IERC725Y(msg.sender).loadTipSettingsRaw();
        (bool decodingSuccess, SettingsLib.TipSettings memory tipSettings, bytes memory decodingError) =
            settingsValue.decodeTipSettings();

        // Decoding also validates the tip settings
        if (!decodingSuccess) return decodingError;

        (bool isEligible, bytes memory eligibilityError) =
            _validateTipEligibilityCriterias(follower, tipSettings.minimumFollowers, tipSettings.minimumPotatoBalance);
        if (!isEligible) return eligibilityError;

        (bool canTransferTip, bytes memory preTransferError) = _validateCanTransferTip(tipSettings.tipAmount);
        if (!canTransferTip) return preTransferError;

        return _transferTip(follower, tipSettings.tipAmount);
    }

    /// @notice Monitor unfollow activity to mark existing followers BPT as ineligible for a tip if they re-follow.
    ///
    /// @dev Re-validate against the LSP26 Follower registry to ensure this function is running in the context of
    /// a legitimate unfollow notification and not from a spoofed call. Existing followers are tracked if we observe
    /// an unfollow without any prior follow registration AFTER the user connected the Potato Tipper (APT).
    ///
    /// @return message A human-readable message to describe the context of the unfollow action.
    function _handleOnUnfollow(address follower) internal returns (bytes memory message) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(follower, msg.sender);
        if (isFollowing) return unicode"‚õìÔ∏è‚Äçüí•‚ùå Not a legitimate unfollow";

        bool isAlreadyTipped = _tippedFollowers[follower][msg.sender];
        if (isAlreadyTipped) return unicode"üëãüèª Already tipped, now unfollowing. Goodbye!";

        // If the follower was not previously registered, consider it as an existing follower that unfollowed
        bool wasFollowerRegisted = _postInstallFollowers[follower][msg.sender];
        if (!wasFollowerRegisted) {
            _existingFollowersUnfollowedPostInstall[follower][msg.sender] = true;
            return unicode"üëãüèª Assuming existing follower is unfollowing. Goodbye!";
        }

        return unicode"üëãüèª Sorry to see you go. Hope you re-follow soon! Goodbye!";
    }

    // Internal helpers
    // ----------------

    /// @dev Check if a follower is eligible to receive a tip according to the provided user's tip settings.
    /// Returns an error message explaining why the follower is not eligible.
    function _validateTipEligibilityCriterias(
        address follower,
        uint256 minimumFollowerCountRequired,
        uint256 minimumPotatoBalanceRequired
    ) internal view returns (bool isEligible, bytes memory errorMessage) {
        if (_FOLLOWER_REGISTRY.followerCount(follower) < minimumFollowerCountRequired) {
            return (false, unicode"üîç‚ùå Not eligible for tip: minimum follower required not met");
        }

        if (_POTATO_TOKEN.balanceOf(follower) < minimumPotatoBalanceRequired) {
            return (false, unicode"üîç‚ùå Not eligible for tip: minimum ü•î balance required not met");
        }

        return (true, "");
    }

    /// @notice Ensure this contract can transfer `tipAmount` of ü•î $POTATO tokens on behalf of the user.
    /// Returns an error message explaining why the tip cannot be transferred.
    ///
    /// @dev These checks are also done inside the Potato token contract (LSP7), but performed earlier to:
    /// 1. Avoid making the follower pay the gas cost of a token transfer reverting (external call + return error data).
    /// 2. Return an early error message and pass it to the `returnedData` of the `UniversalReceiver` event.
    function _validateCanTransferTip(uint256 tipAmount)
        internal
        view
        returns (bool canTransferTip, bytes memory errorMessage)
    {
        if (_POTATO_TOKEN.balanceOf(msg.sender) < tipAmount) {
            return (false, unicode"‚öôÔ∏è‚ö†Ô∏è Not enough ü•î left in user's balance");
        }

        if (_POTATO_TOKEN.authorizedAmountFor(address(this), msg.sender) < tipAmount) {
            return (false, unicode"‚öôÔ∏è‚ö†Ô∏è Not enough ü•î left in user's tipping budget");
        }

        return (true, "");
    }

    /// @notice Transfer `tipAmount` of ü•î $POTATO tokens to the new `follower`.
    ///
    /// @dev Use `try {} catch {}` to transfer the tip to prevent any sub-calls from making the whole call revert.
    /// Returns a success or error message to describe if the tip transfer was successful or not.
    function _transferTip(address follower, uint256 tipAmount) internal returns (bytes memory successOrErrorMessage) {
        _tippedFollowers[follower][msg.sender] = true;

        try _POTATO_TOKEN.transfer({
            from: msg.sender, // üÜô that was ‚¨ÖÔ∏è followed
            to: follower, // üÜô that is following ‚û°Ô∏è
            amount: tipAmount, // amount of ü•îü•îü•î to tip
            force: false, // Default to false, but already checked that follower is a üÜô, so we know it supports LSP1
            data: unicode"Thanks for following! Tipping you some ü•î" // context for the token transfer
        }) {
            emit PotatoTipSent({from: msg.sender, to: follower, amount: tipAmount});
            return abi.encodePacked(unicode"ü•î‚úÖ Successfully sent tip to new follower: ", follower.toHexString());
        } catch (bytes memory errorData) {
            // If the token transfer failed (because the call to the `universalReceiver(...)` function reverted when
            // notifying sender/recipient, or any sub-calls), revert state and mark the follower as not tipped.
            delete _tippedFollowers[follower][msg.sender];

            emit PotatoTipFailed({from: msg.sender, to: follower, amount: tipAmount, errorData: errorData});
            return abi.encodePacked(
                unicode"ü•î‚ùå Failed to send tip to ", follower.toHexString(), ". LSP7 transfer reverted"
            );
        }
    }
}
