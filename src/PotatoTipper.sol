// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.28;

// interfaces
import {IERC725Y} from "@erc725/smart-contracts/contracts/interfaces/IERC725Y.sol";
import {IERC165} from "@openzeppelin/contracts/interfaces/IERC165.sol";
import {
    ILSP1UniversalReceiverDelegate as ILSP1Delegate
} from "@lukso/lsp1-contracts/contracts/ILSP1UniversalReceiverDelegate.sol";

// libraries
import {ERC165Checker} from "@openzeppelin/contracts/utils/introspection/ERC165Checker.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import {PotatoLib} from "./PotatoLib.sol";

// constants
import {_INTERFACEID_LSP0} from "@lukso/lsp0-contracts/contracts/LSP0Constants.sol";
import {_INTERFACEID_LSP1_DELEGATE} from "@lukso/lsp1-contracts/contracts/LSP1Constants.sol";
import {_TYPEID_LSP26_FOLLOW, _TYPEID_LSP26_UNFOLLOW} from "@lukso/lsp26-contracts/contracts/LSP26Constants.sol";
import {POTATO_TIPPER_SETTINGS_DATA_KEY, _FOLLOWER_REGISTRY, _POTATO_TOKEN} from "./Constants.sol";

// events
import {TipSent, TipFailed} from "./Events.sol";

// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚£Ä‚°§‚£î‚¢≤‚°í‚¢¶‚°ô‚°¥‚£í‚£ñ‚°†‚£Ñ‚£Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚°¥‚°û‚°π‚¢Ü‚£ù‚£§‚££‚°ô‚¢¶‚£ô‚°¥‚°°‚¢¶‚°ô‚£±‚†∫‚£≠‚£ñ‚†§‚°Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚£¥‚†ª‚£°‚¢≥‚†µ‚†õ‚†â‚†Ä‚†Ä‚†Ä‚°Ä‚¢Ä‚†Ä‚°à‚†ô‚¢¢‚°ù‚°§‚¢ì‚†¶‚°ú‚†ª‚£ú‚°¢‚£Ñ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚°¥‚£õ‚†¨‚££‚†ã‚†Å‚¢Ä‚††‚†ê‚†à‚°Ä‚¢Å‚†Ä‚†Ç‚††‚†ê‚†Ä‚†Ñ‚°ø‚£ê‚°ü‚†â‚†â‚†≥‚£å‚†≥‚£ú‚¢¢‚°Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢†‚°û‚°∏‚£§‚†ü‚†Ä‚†Ä‚†å‚¢Ä‚†Ä‚†Ç‚†ê‚†Ä‚†Ñ‚†à‚†Ñ‚¢Å‚£Ñ‚°¨‚¢û‚°±‚£°‚¢õ‚£§‚£ê‚£Ä‚£º‚†≥‚°å‚¢ß‚°±‚°Ñ‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢†‚°≥‚¢ç‚°∂‚†Å‚†Ä‚†Ñ‚†°‚¢Ä‚£¢‚†¨‚°¥‚¢ì‚°û‚¢≤‚†´‚°ù‚¢≠‚†¢‚°ù‚¢¢‚°ì‚†¥‚£É‚¢Ü‚°£‚°ç‚¢¶‚†ì‚°º‚¢°‚†≥‚£∏‚°Ñ‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚†∑‚£©‚†û‚†Ä‚††‚¢Å‚°¥‚°∫‚¢ç‚°≤‚£ë‚†é‚°µ‚°®‚¢á‚¢≥‚†∏‚£°‚†ì‚£ç‚¢∑‚£Æ‚¢ì‚°ú‚£¢‚¢µ‚°™‚£•‚†õ‚£î‚°ã‚£∑‚°á‚£∑‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚¢æ‚†£‚°è‚°Ä‚†Ñ‚£°‚°è‚¢ñ‚°©‚¢ñ‚°±‚¢ú‚¢™‚†±‚£±‚¢ä‚†ß‚£ô‚°î‚¢´‚°î‚¢´‚°±‚¢é‚†¥‚£É‚†æ‚£Ω‚£∂‚£ã‚¢¶‚°π‚¢ø‚°õ‚£ß‚°á‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚¢Æ‚†£‚£ù‚†≥‚°¥‚°ö‚¢ß‚£ò‚¢£‚†ú‚¢¶‚°ô‚°¨‚¢é‚†µ‚£Ç‚¢è‚†≤‚£Ö‚†∫‚£°‚¢é‚¢£‚°ú‚£ä‚†∂‚°ë‚£é‚¢π‚¢∫‚£ª‚£Æ‚°ù‚¢¶‚°ô‚£∑‚£ª‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚¢Ä‚°∞‚¢è‚†é‚£ï‚¢™‚£±‚¢£‚°ô‚¢Ü‚†Æ‚°ú‚¢™‚°±‚¢ú‚¢¢‚£ù‚¢¢‚°ç‚¢é‚°ï‚°™‚¢ï‚°≤‚¢å‚°£‚¢ú‚¢¢‚¢á‚°π‚¢§‚¢£‚†ì‚£é‚£õ‚†ø‚¢¶‚¢π‚£∑‚°π‚°Ñ
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£Ä‚°¥‚¢´‚°ô‚£¨‚†ö‚£å‚†¶‚°π‚¢ü‚£ª‚°ø‚¢∂‚£ç‚¢£‚°ú‚¢™‚°±‚¢è‚°£‚°ú‚¢é‚°¥‚°ô‚¢¶‚†±‚¢é‚°±‚°©‚¢ñ‚¢™‚°ë‚£é‚†≤‚£ç‚†≤‚°å‚¢û‚¢¢‚£ª‚¢û‚°µ‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚°æ‚¢£‚¢ç‚°£‚°ú‚†¥‚°ô‚¢Ü‚°≥‚¢°‚†è‚°¥‚¢©‚£ã‚†ú‚£Ü‚°ö‚¢•‚¢ö‚°¥‚°ë‚¢Æ‚°ë‚¢¶‚°ô‚¢Ü‚†Ø‚£ò‚†≤‚£Ö‚†´‚¢Ü‚†≥‚£å‚†≥‚£∏‚£∑‚£è‚¢é‚°±‚£Ø‚°ª‚£ú‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚£†‚£æ‚¢ü‚°¥‚£ã‚†¶‚°±‚¢é‚¢£‚†ù‚°∏‚°î‚¢´‚¢ú‚°∏‚¢Ö‚°é‚†û‚£§‚†π‚£ò‚†¶‚£í‚†≠‚°í‚£ç‚†¶‚£ô‚†é‚£ú‚£°‚†≥‚£å‚†≥‚£â‚†≥‚£å‚†≥‚£©‚¢õ‚†ª‚°å‚£æ‚°≥‚£ù‚¢ß‚°á
// ‚†Ä‚†Ä‚†Ä‚†Ä‚°¥‚¢ü‚°π‚£ª‚†ø‚°é‚¢ñ‚°±‚°©‚¢é‚£ö‚¢±‚£Æ‚†á‚£é‚†ñ‚£©‚†ú‚£±‚¢ä‚†µ‚°°‚¢û‚°∞‚¢£‚°ô‚£§‚¢ã‚¢¶‚°ô‚¢Ü‚°ñ‚°±‚£ä‚†µ‚£â‚†∂‚£°‚†ì‚°•‚¢é‚¢≥‚¢∏‚£∑‚¢´‚°Ω‚£∫‚†Ö
// ‚†Ä‚†Ä‚¢Ä‚¢Æ‚°ô‚£Ü‚¢£‚†µ‚°©‚¢ú‚†£‚£ú‚£°‚†≥‚£å‚†£‚£ç‚°ö‚°§‚¢õ‚°§‚¢õ‚¢§‚°ã‚°º‚°ë‚£é‚†±‚¢£‚°±‚¢Ü‚¢≠‚†¢‚°ù‚†≤‚¢¨‚°±‚¢ú‚°∏‚¢å‚†∂‚£°‚¢ã‚¢ñ‚°©‚¢é‚°ø‚£é‚¢∑‚°π‚£Ω‚†Ä
// ‚†Ä‚†Ä‚£º‚¢ç‚†ñ‚£±‚¢ä‚°ñ‚°ç‚£é‚†≥‚°∞‚¢Ü‚†≥‚£å‚¢ì‚†¶‚£±‚†©‚¢ñ‚°°‚¢è‚†¶‚£±‚¢°‚†≥‚°å‚°≠‚¢£‚¢ú‚°ä‚°ñ‚†≠‚°ú‚£ô‚†¶‚°±‚¢é‚°ú‚£ä‚†∂‚°°‚†û‚£å‚†ñ‚£ø‚£ù‚£Æ‚¢≥‚¢Ø‚°ç‚†Ä
// ‚†Ä‚¢∏‚£ª‚¢ú‚¢™‚°ë‚°é‚°¥‚¢ì‚°å‚¢á‚°ì‚¢é‚†≥‚£å‚°ö‚°ú‚†¥‚£ô‚¢¨‚°ö‚¢¨‚†≤‚£Ö‚¢é‚†≥‚¢¨‚£ë‚†£‚£é‚†ú‚°ú‚°•‚°ô‚¢Ü‚£ß‚°ì‚°º‚£ê‚¢£‚†é‚°µ‚¢©‚¢Ü‚£ø‚°ª‚£º‚£é‚£ü‚£û‚†É‚†Ä
// ‚†Ä‚£ü‚£ø‚°ò‚£Ü‚¢£‚°ï‚¢é‚°±‚¢™‚°ë‚¢Æ‚†©‚°ñ‚£°‚†û‚£å‚†≥‚°ú‚£∂‚£Ω‚£¶‚£ì‚¢¨‚¢ä‚°ù‚¢¢‚†é‚°µ‚°ò‚¢é‚°±‚°ú‚£©‚¢é‚¢ª‚†±‚°í‚°ç‚¢¶‚¢ã‚°¥‚¢ã‚£º‚£ó‚£ª‚£ø‚£ø‚°û‚°º‚†Ä‚†Ä
// ‚¢∏‚£Ω‚¢æ‚°±‚°å‚†∂‚°ò‚¢é‚°±‚¢£‚°ô‚¢Ü‚°è‚†¥‚£É‚†û‚£å‚†≥‚£ò‚°å‚¢≥‚†Ω‚£ª‚¢æ‚£Æ‚¢ú‚°°‚¢è‚°¥‚°ô‚£¨‚†±‚°ú‚°î‚°™‚¢•‚¢ã‚°ï‚¢Æ‚°ë‚†Æ‚£î‚°ø‚£≥‚¢é‚°∑‚£π‚¢∂‚£π‚†É‚†Ä‚†Ä
// ‚¢∏‚£û‚¢ß‚£∑‚¢â‚°û‚°©‚¢Æ‚£µ‚°£‚¢é‚¢£‚°ú‚†≥‚°å‚†û‚£å‚¢£‚†ï‚£ä‚¢á‚†Æ‚£ë‚¢´‚°ô‚¢¶‚°ô‚¢Ü‚°ñ‚°ç‚£Ü‚†≥‚°ú‚°∏‚£ë‚¢é‚°±‚¢ä‚¢¶‚°ô‚£º‚¢û‚°≥‚£ù‚¢æ‚°±‚£è‚°û‚°è‚†Ä‚†Ä‚†Ä
// ‚†∏‚£æ‚£è‚†æ‚£ß‚£ò‚†±‚¢´‚°ô‚£•‚¢ã‚¢ñ‚£ò‚¢£‚†≠‚£ô‚¢§‚°ã‚°º‚¢°‚¢é‚†≥‚£å‚¢£‚°ú‚¢¶‚°ô‚°≤‚†∏‚°î‚¢£‚°ì‚£ú‚£±‚£¨‚†í‚°≠‚£©‚¢Ü‚£Ω‚¢≥‚¢Ø‚°ù‚£Æ‚¢≥‚°ù‚£Æ‚°ù‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚£∑‚¢´‚°ü‚°Ω‚£Ü‚¢è‚†•‚£ì‚¢§‚¢ã‚°ñ‚°∏‚°å‚†∂‚£â‚¢¶‚†±‚£±‚°â‚¢Æ‚¢±‚°ò‚°Ü‚†û‚£§‚†ì‚£ç‚¢£‚†ù‚£¢‚†ï‚°∫‚¢Ω‚†ª‚£ç‚¢≤‚£±‚†æ‚£≠‚£õ‚¢Æ‚£ù‚¢Æ‚°≥‚°Ω‚°û‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚¢∏‚£ª‚£ú‚°≥‚£ù‚°ª‚£î‚¢£‚†é‚£ñ‚†∏‚£±‚¢ò‚°£‚¢ï‚°™‚†ï‚¢¶‚°ô‚¢Ü‚°á‚¢û‚°∏‚£â‚¢¶‚†π‚°å‚¢é‚¢é‚°±‚¢é‚°±‚¢é‚°±‚°º‚°æ‚£≠‚£õ‚¢∂‚°π‚£û‚°º‚££‚¢ü‚°ù‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚¢∑‚£´‚†∑‚£≠‚¢≥‚£è‚¢∑‚¢æ‚£à‚°ì‚†¶‚£ç‚°í‚†ß‚°ú‚£ô‚†¶‚°ô‚¢¶‚£ø‚°¶‚¢±‚¢ä‚¢¶‚¢ã‚°º‚£â‚†¶‚°ì‚£¨‚£±‚¢æ‚°π‚£è‚¢∑‚££‚¢ü‚£Æ‚¢≥‚°ù‚£æ‚£±‚†è‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†à‚¢Ø‚£ü‚°º‚£≥‚¢é‚°ü‚£Æ‚¢Ø‚°Ω‚£≥‚¢¶‚£ô‚°ú‚°ú‚°¢‚¢ù‚°ò‚¢¶‚°ô‚°¥‚¢ã‚°ú‚£¢‚¢ç‚¢≤‚£°‚†æ‚£µ‚¢´‚°û‚£ß‚¢ª‚°º‚£ø‚£ø‚°æ‚£ú‚¢ß‚°ª‚£∂‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†à‚¢ø‚¢æ‚°µ‚£õ‚†æ‚£µ‚£ø‚£æ‚£≠‚¢Ø‚°ù‚£æ‚£π‚¢≥‚°ü‚£û‚¢¶‚°≥‚£ú‚°≥‚£û‚¢∂‚£´‚¢ü‚°º‚£ª‚£º‚£≥‚¢ª‚£º‚££‚†ø‚£Ω‚£õ‚¢∑‚°π‚£Æ‚†ü‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†ª‚£Ω‚£Ø‚£ü‚£ø‚£ø‚°ø‚£è‚¢æ‚°π‚¢∂‚£≠‚¢≥‚°ù‚£Æ‚¢≥‚°ù‚£ß‚¢ª‚°ú‚£ß‚£õ‚¢Æ‚£≥‚¢≥‚¢æ‚£ª‚¢ü‚£æ‚£Ω‚£õ‚°∂‚£π‚¢Æ‚†ø‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†ª‚£ø‚£∑‚£π‚¢û‚°Ω‚¢Æ‚£ù‚°≥‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£é‚¢∑‚£ø‚£ß‚£ü‚¢Æ‚£≥‚£õ‚°æ‚£ù‚°ª‚£û‚£Ω‚¢ø‚°Ω‚†ü‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†≥‚¢ª‚°ø‚£º‚°≥‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£é‚¢∑‚°π‚£æ‚£ø‚£ø‚¢ø‚£´‚£ø‚£ø‚°ú‚£ß‚¢ü‚°æ‚†ú‚†ã‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†â‚†ì‚†ø‚£π‚°û‚°µ‚¢Ø‚°û‚£µ‚£´‚£û‚£µ‚£≥‚°û‚£º‚¢£‚°∑‚£ª‚°º‚†Ω‚†ö‚†â‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä
// ‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†à‚†â‚†ë‚†õ‚†í‚†õ‚†ö‚†ì‚†ì‚†õ‚†ä‚†â‚†â‚†Ä‚†Å‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä‚†Ä

/**
 * @title The PotatoTipper contract allows a üÜô to react when receiving a new follower,
 * and tip ü•î $POTATO tokens to this new follower. Can be used as an automated incentive mechanism.
 *
 * @author Jean Cavallera (CJ42)
 *
 * @dev Terminology:
 * - BPT = "Before Potato Tipper" = for followers that followed a user before it connected the Potato Tipper
 * - APT = "After Potato Tipper" = for followers that followed a user after it connected the Potato Tipper"
 *
 * @notice ‚ö†Ô∏è Disclaimer: this contract has not been formally audited by an external third party
 * auditor. The contract does not guarantee to be bug free. Use responsibly at your own risk.
 */
contract PotatoTipper is IERC165, ILSP1Delegate {
    using ERC165Checker for address;
    using Strings for address;
    using PotatoLib for bytes;

    /// @dev Track `follower` addresses that received a tip already from a `user`'s UP
    mapping(address user => mapping(address follower => bool tippedAPT)) internal _tipped;

    /// @dev Track `follower` addresses that followed a user's üÜô AFTER the Potato Tipper was connected
    /// Regardless if the follower received a tip or not
    mapping(address user => mapping(address follower => bool followedAPT)) internal _hasFollowedSinceDelegate;

    /// @dev Track followers that existed BEFORE the Potato Tipper was connected to the user's UP
    /// (observed via an unfollow notifications without any post-install follow ever observed)
    mapping(address user => mapping(address follower => bool followedBPT)) internal _wasFollowing;

    /// @notice Check if the contract implements a given interface
    ///
    /// @dev Only LSP1Delegate and ERC165 interfaces are supported
    ///
    /// @param interfaceId The interface identifier, as specified in ERC-165
    /// @return true if the contract implements `interfaceId`, false otherwise
    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return interfaceId == _INTERFACEID_LSP1_DELEGATE || interfaceId == type(IERC165).interfaceId;
    }

    /// Read functions
    /// ---------------

    /// @notice Check if a `follower` address has already been attempted to be tippied from a `user`'s UP
    ///
    /// @dev The result of this function does not guarantee that the `follower` actually received a tip,
    /// only that the `user`'s UP attempted to send a tip to the new `follower`. This is because the tip transfer
    /// could have failed for various reasons during the LSP1 `universalReceiver(...)` hook call on the
    /// `follower` and `user`'s UPs.
    ///
    /// @param follower The address of the follower that has been tipped
    /// @param user The address of the user that sent the tip
    ///
    /// @return true if the `follower` has already been tipped by the `user`, false otherwise
    function hasBeenTipped(address follower, address user) external view returns (bool) {
        return _tipped[user][follower];
    }

    /// @notice Determines if a `follower` address was already following a `user`'s UP before the user
    /// connected
    /// it's UP to the Potato Tipper contract. Helps to define if a follower is eligible for a tip or not.
    ///
    /// @dev This is determined by observing if an unfollow notification was received from the
    /// LSP26 Follower Registry without any prior follow notification being observed since the
    /// Potato Tipper was connected to the user's UP.
    ///
    /// @param follower The address of the follower that has been followed
    /// @param user The address of the user that was followed
    ///
    /// @return true if `follower` was already following `user` before it connected to the Potato Tipper.
    function wasFollowingBeforePotatoTipper(address follower, address user) external view returns (bool) {
        return _wasFollowing[user][follower];
    }

    /// @notice Check if a `follower` address has followed a `user`'s UP after `user` connected its üÜô
    /// to the Potato Tipper contract.
    ///
    /// @param follower The address of the follower that followed `user`.
    /// @param user The address of the user that was followed.
    /// @return true if `follower` followed `user` after it connected to the Potato Tipper, false otherwise.
    function followedAfterPotatoTipper(address follower, address user) external view returns (bool) {
        return _hasFollowedSinceDelegate[user][follower];
    }

    /// Write functions
    /// ---------------

    /// @notice Handle follow/unfollow notifications + automatically tip ü•î  tokens to new follower
    ///
    /// @dev Called by user's üÜô `universalReceiver(...)` function when receiving a notification from the
    /// LSP26 Follower Registry about a new follower or an unfollow action (extracted from notification `data`).
    ///
    /// @param sender The address that notified the user's UP (MUST be the LSP26 Follower Registry)
    /// @param typeId The type ID of the notification (follow or unfollow)
    /// @param data Sent by the LSP26 Follower registry when notifying user (MUST be a 20 bytes long address)
    ///
    /// @return message A human-readable message that can be decoded from the `UniversalReceiver` event log
    ///
    // solhint-disable-next-line use-natspec
    function universalReceiverDelegate(
        address sender,
        uint256, /* value */
        bytes32 typeId,
        bytes calldata data
    )
        external
        returns (bytes memory)
    {
        // CHECK that this call came from the Follower Registry
        if (sender != address(_FOLLOWER_REGISTRY)) return unicode"‚ùå Not triggered by the Follower Registry";

        // Retrieve follower address from the notification data sent by the LSP26 Follower Registry
        if (data.length != 20) return unicode"‚ùå Invalid data received. Must be a 20 bytes long address";
        // casting to 'bytes20' is safe because of check above
        // forge-lint: disable-next-line(unsafe-typecast)
        address follower = address(bytes20(data));

        // Only üÜô‚úÖ allowed to receive tips, üîë‚ùå not EOAs
        if (!follower.supportsInterface(_INTERFACEID_LSP0)) return unicode"‚ùå Only üÜô allowed to be tipped";

        // CHECK notification type ID and only run if we are being notified about follow / unfollow actions
        if (typeId == _TYPEID_LSP26_FOLLOW) return _onFollow(follower);
        if (typeId == _TYPEID_LSP26_UNFOLLOW) return _onUnfollow(follower);

        return unicode"‚ùå Not a follow or unfollow notification";
    }

    /// Internal functions
    /// ------------------

    /// @notice Handle a new follower notification and tip ü•î $POTATO tokens if eligible
    /// @dev This function performs various checks to ensure the follow notification is legitimate,
    /// including verifying if the follower has not already been tipped.
    /// Note that existing followers BPT are not eligible for tips.
    ///
    /// @param follower The address of the new follower that followed the user's UP
    ///
    /// @return message A human-readable message returned to the `universalReceiver(...)` function, to
    /// indicate successful tip, or an error reason if no tip was sent.
    /// This message can be decoded from the `UniversalReceiver` event log
    function _onFollow(address follower) internal returns (bytes memory message) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(follower, msg.sender);

        // CHECK to ensure this came from a legitimate notification callback from the LSP26 Registry
        if (!isFollowing) return unicode"‚ùå Not a legitimate follow";

        // Record when we see a new follower AFTER the PotatoTipper was connected to user's üÜô
        if (!_hasFollowedSinceDelegate[msg.sender][follower]) {
            _hasFollowedSinceDelegate[msg.sender][follower] = true;
        }

        // CHECK user has not already received a tip after following
        // (prevent recursive follow -> unfollow -> re-follow ü•î üöú)
        if (_tipped[msg.sender][follower]) return unicode"üôÖüèª Already tipped a potato";

        // Check this is not an existing follower that unfollowed and tried to re-follow
        if (_wasFollowing[msg.sender][follower]) {
            return unicode"üôÖüèª Follower followed before. Not eligible for a tip";
        }

        return _sendTip(follower);
    }

    /// @notice Handle an unfollow notification
    /// @dev This function is used to track existing followers that unfollow the user's UP.
    /// To prevent existing followers from unfollowing -> re-following to try to get tips.
    ///
    /// @param address_ The address that unfollowed the user's UP
    /// @return message A human-readable message returned to the `universalReceiver(...)` function.
    function _onUnfollow(address address_) internal returns (bytes memory) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(address_, msg.sender);

        // CHECK to ensure this came from a legitimate notification callback from the LSP26 Registry
        if (isFollowing) return unicode"‚ùå Not a legitimate unfollow";

        // Don't do anything if follower already received a tip (legitimate unfollow APT)
        if (_tipped[msg.sender][address_]) return "";

        // If `address_` never followed the user after it connected the Potato Tipper,
        // this proves that `address_` was an existing follower at install time BPT.
        //
        // Handle cases of existing followers unfollowing -> then re-following to try to get a tip
        // Lock them out and prevent from tipping them if they try to re-follow.
        if (!_hasFollowedSinceDelegate[msg.sender][address_]) {
            _wasFollowing[msg.sender][address_] = true;
            return "";
        }

        // Allow new followers to unfollow -> re-follow to try to get a tip again
        // (e.g: if tipped failed because not enough ü•î in user's balance, tipping budget, or transfer
        // failed). This allows an `address_` APT to re-follow and still be eligible for a tip.
        return "";
    }

    /// @notice Internal function to send ü•î $POTATO tokens as tips to the new follower
    ///
    /// @dev This function fetch the tip settings from the ERC725Y storage of the user's üÜô,
    /// and performs pre-checks (e.g: sufficient balance, allowance, etc...) to ensure the tip can be sent.
    /// Tipping is handled via `try {} catch {}` and emit `TipSent` or `TipFailed` events.
    /// Note that if the $POTATO token transfer fails due to any nested calls to the `universalReceiver(...)`
    /// function of the `follower` or `user`'s UP reverting, the follower will still be marked as having been
    /// tipped. See function `hasBeenTipped(...)` above for details.
    ///
    /// @param follower The address of the new follower that will receive a tip
    /// @return A human-readable message that can be decoded from the `UniversalReceiver` event
    function _sendTip(address follower) internal returns (bytes memory) {
        // Fetch configs from user's UP metadata
        bytes memory settingsValue = IERC725Y(msg.sender).getData(POTATO_TIPPER_SETTINGS_DATA_KEY);

        (bool decodingSuccess, uint256 tipAmount, uint256 minimumFollowers, uint256 minimumPotatoBalance) =
            settingsValue.decodeSettings();

        // CHECK if the settings value is valid
        if (!decodingSuccess) {
            return unicode"‚ùå Invalid settings value. Must be 96 bytes long encoded as (uint256,uint256,uint256)";
        }

        // CHECK the address being followed has enough ü•î to tip.
        if (_POTATO_TOKEN.balanceOf(msg.sender) < tipAmount) {
            return unicode"ü§∑üèª‚Äç‚ôÇÔ∏è Not enough ü•î left in balance";
        }

        // CHECK if the Potato Tipper contract has enough left in its tipping budget
        if (_POTATO_TOKEN.authorizedAmountFor(address(this), msg.sender) < tipAmount) {
            return unicode"‚ùå Not enough ü•î left in tipping budget";
        }

        // CHECK the follower has the minimum number of followers required
        if (_FOLLOWER_REGISTRY.followerCount(follower) < minimumFollowers) {
            return unicode"‚ùå Not eligible for tip: minimum follower required not met";
        }

        // CHECK if the followers has the minimum amount of $POTATO tokens required
        if (_POTATO_TOKEN.balanceOf(follower) < minimumPotatoBalance) {
            return unicode"‚ùå Not eligible for tip: minimum ü•î balance required not met";
        }

        _tipped[msg.sender][follower] = true;

        // Transfer ü•î $POTATO ü•î tokens as tip to the new follower
        // Return a success or error message that can be decoded from the `UniversalReceiver` event
        try _POTATO_TOKEN.transfer({
            // üÜô that was ‚¨ÖÔ∏è followed
            from: msg.sender,
            // üÜô that is following ‚û°Ô∏è
            to: follower,
            // amount of ü•îü•îü•î to tip
            amount: tipAmount,
            // Default to false, but we already checked if follower is a üÜô, so we know it supports LSP1
            force: false,
            // message data to give context to the LSP7 token transfer
            data: unicode"Thanks for following! Tipping you some ü•î"
        }) {
            emit TipSent({from: msg.sender, to: follower, amount: tipAmount});
            return abi.encodePacked(unicode"‚úÖ Successfully tipped üç† to new follower: ", follower.toHexString());
        } catch (bytes memory errorData) {
            emit TipFailed({from: msg.sender, to: follower, amount: tipAmount, errorData: errorData});
            return unicode"‚ùå Failed tipping ü•î. LSP7 transfer reverted";
        }
    }
}
