// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.28;

// modules
import {PotatoTipperConfig} from "./PotatoTipperConfig.sol";

// interfaces
import {IERC725Y} from "@erc725/smart-contracts/contracts/interfaces/IERC725Y.sol";
import {IERC165} from "@openzeppelin/contracts/interfaces/IERC165.sol";
import {
    ILSP1UniversalReceiverDelegate as ILSP1Delegate
} from "@lukso/lsp1-contracts/contracts/ILSP1UniversalReceiverDelegate.sol";

// libraries
import {ERC165Checker} from "@openzeppelin/contracts/utils/introspection/ERC165Checker.sol";
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";
import "./PotatoTipperSettingsLib.sol" as SettingsLib;

// constants
import {_INTERFACEID_LSP0} from "@lukso/lsp0-contracts/contracts/LSP0Constants.sol";
import {_TYPEID_LSP26_FOLLOW, _TYPEID_LSP26_UNFOLLOW} from "@lukso/lsp26-contracts/contracts/LSP26Constants.sol";
import {_FOLLOWER_REGISTRY, _POTATO_TOKEN} from "./Constants.sol";

// events
import {PotatoTipSent, PotatoTipFailed} from "./Events.sol";

// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â£€â¡¤â£”â¢²â¡’â¢¦â¡™â¡´â£’â£–â¡ â£„â£€â €â €â €â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â£ â¡´â¡â¡¹â¢†â£â£¤â££â¡™â¢¦â£™â¡´â¡¡â¢¦â¡™â£±â ºâ£­â£–â ¤â¡€â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â£´â »â£¡â¢³â µâ ›â ‰â €â €â €â¡€â¢€â €â¡ˆâ ™â¢¢â¡â¡¤â¢“â ¦â¡œâ »â£œâ¡¢â£„â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â¡´â£›â ¬â££â ‹â â¢€â  â â ˆâ¡€â¢â €â ‚â  â â €â „â¡¿â£â¡Ÿâ ‰â ‰â ³â£Œâ ³â£œâ¢¢â¡€â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢ â¡â¡¸â£¤â Ÿâ €â €â Œâ¢€â €â ‚â â €â „â ˆâ „â¢â£„â¡¬â¢â¡±â£¡â¢›â£¤â£â£€â£¼â ³â¡Œâ¢§â¡±â¡„â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢ â¡³â¢â¡¶â â €â „â ¡â¢€â£¢â ¬â¡´â¢“â¡â¢²â «â¡â¢­â ¢â¡â¢¢â¡“â ´â£ƒâ¢†â¡£â¡â¢¦â “â¡¼â¢¡â ³â£¸â¡„â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â ·â£©â â €â  â¢â¡´â¡ºâ¢â¡²â£‘â â¡µâ¡¨â¢‡â¢³â ¸â£¡â “â£â¢·â£®â¢“â¡œâ£¢â¢µâ¡ªâ£¥â ›â£”â¡‹â£·â¡‡â£·â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â¢¾â £â¡â¡€â „â£¡â¡â¢–â¡©â¢–â¡±â¢œâ¢ªâ ±â£±â¢Šâ §â£™â¡”â¢«â¡”â¢«â¡±â¢â ´â£ƒâ ¾â£½â£¶â£‹â¢¦â¡¹â¢¿â¡›â£§â¡‡â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â¢€â¢®â £â£â ³â¡´â¡šâ¢§â£˜â¢£â œâ¢¦â¡™â¡¬â¢â µâ£‚â¢â ²â£…â ºâ£¡â¢â¢£â¡œâ£Šâ ¶â¡‘â£â¢¹â¢ºâ£»â£®â¡â¢¦â¡™â£·â£»â €
// â €â €â €â €â €â €â €â €â €â €â €â¢€â¡°â¢â â£•â¢ªâ£±â¢£â¡™â¢†â ®â¡œâ¢ªâ¡±â¢œâ¢¢â£â¢¢â¡â¢â¡•â¡ªâ¢•â¡²â¢Œâ¡£â¢œâ¢¢â¢‡â¡¹â¢¤â¢£â “â£â£›â ¿â¢¦â¢¹â£·â¡¹â¡„
// â €â €â €â €â €â €â €â €â €â£€â¡´â¢«â¡™â£¬â šâ£Œâ ¦â¡¹â¢Ÿâ£»â¡¿â¢¶â£â¢£â¡œâ¢ªâ¡±â¢â¡£â¡œâ¢â¡´â¡™â¢¦â ±â¢â¡±â¡©â¢–â¢ªâ¡‘â£â ²â£â ²â¡Œâ¢â¢¢â£»â¢â¡µâ¡‡
// â €â €â €â €â €â €â €â£ â¡¾â¢£â¢â¡£â¡œâ ´â¡™â¢†â¡³â¢¡â â¡´â¢©â£‹â œâ£†â¡šâ¢¥â¢šâ¡´â¡‘â¢®â¡‘â¢¦â¡™â¢†â ¯â£˜â ²â£…â «â¢†â ³â£Œâ ³â£¸â£·â£â¢â¡±â£¯â¡»â£œâ¡‡
// â €â €â €â €â €â£ â£¾â¢Ÿâ¡´â£‹â ¦â¡±â¢â¢£â â¡¸â¡”â¢«â¢œâ¡¸â¢…â¡â â£¤â ¹â£˜â ¦â£’â ­â¡’â£â ¦â£™â â£œâ£¡â ³â£Œâ ³â£‰â ³â£Œâ ³â£©â¢›â »â¡Œâ£¾â¡³â£â¢§â¡‡
// â €â €â €â €â¡´â¢Ÿâ¡¹â£»â ¿â¡â¢–â¡±â¡©â¢â£šâ¢±â£®â ‡â£â –â£©â œâ£±â¢Šâ µâ¡¡â¢â¡°â¢£â¡™â£¤â¢‹â¢¦â¡™â¢†â¡–â¡±â£Šâ µâ£‰â ¶â£¡â “â¡¥â¢â¢³â¢¸â£·â¢«â¡½â£ºâ …
// â €â €â¢€â¢®â¡™â£†â¢£â µâ¡©â¢œâ £â£œâ£¡â ³â£Œâ £â£â¡šâ¡¤â¢›â¡¤â¢›â¢¤â¡‹â¡¼â¡‘â£â ±â¢£â¡±â¢†â¢­â ¢â¡â ²â¢¬â¡±â¢œâ¡¸â¢Œâ ¶â£¡â¢‹â¢–â¡©â¢â¡¿â£â¢·â¡¹â£½â €
// â €â €â£¼â¢â –â£±â¢Šâ¡–â¡â£â ³â¡°â¢†â ³â£Œâ¢“â ¦â£±â ©â¢–â¡¡â¢â ¦â£±â¢¡â ³â¡Œâ¡­â¢£â¢œâ¡Šâ¡–â ­â¡œâ£™â ¦â¡±â¢â¡œâ£Šâ ¶â¡¡â â£Œâ –â£¿â£â£®â¢³â¢¯â¡â €
// â €â¢¸â£»â¢œâ¢ªâ¡‘â¡â¡´â¢“â¡Œâ¢‡â¡“â¢â ³â£Œâ¡šâ¡œâ ´â£™â¢¬â¡šâ¢¬â ²â£…â¢â ³â¢¬â£‘â £â£â œâ¡œâ¡¥â¡™â¢†â£§â¡“â¡¼â£â¢£â â¡µâ¢©â¢†â£¿â¡»â£¼â£â£Ÿâ£â ƒâ €
// â €â£Ÿâ£¿â¡˜â£†â¢£â¡•â¢â¡±â¢ªâ¡‘â¢®â ©â¡–â£¡â â£Œâ ³â¡œâ£¶â£½â£¦â£“â¢¬â¢Šâ¡â¢¢â â¡µâ¡˜â¢â¡±â¡œâ£©â¢â¢»â ±â¡’â¡â¢¦â¢‹â¡´â¢‹â£¼â£—â£»â£¿â£¿â¡â¡¼â €â €
// â¢¸â£½â¢¾â¡±â¡Œâ ¶â¡˜â¢â¡±â¢£â¡™â¢†â¡â ´â£ƒâ â£Œâ ³â£˜â¡Œâ¢³â ½â£»â¢¾â£®â¢œâ¡¡â¢â¡´â¡™â£¬â ±â¡œâ¡”â¡ªâ¢¥â¢‹â¡•â¢®â¡‘â ®â£”â¡¿â£³â¢â¡·â£¹â¢¶â£¹â ƒâ €â €
// â¢¸â£â¢§â£·â¢‰â¡â¡©â¢®â£µâ¡£â¢â¢£â¡œâ ³â¡Œâ â£Œâ¢£â •â£Šâ¢‡â ®â£‘â¢«â¡™â¢¦â¡™â¢†â¡–â¡â£†â ³â¡œâ¡¸â£‘â¢â¡±â¢Šâ¢¦â¡™â£¼â¢â¡³â£â¢¾â¡±â£â¡â¡â €â €â €
// â ¸â£¾â£â ¾â£§â£˜â ±â¢«â¡™â£¥â¢‹â¢–â£˜â¢£â ­â£™â¢¤â¡‹â¡¼â¢¡â¢â ³â£Œâ¢£â¡œâ¢¦â¡™â¡²â ¸â¡”â¢£â¡“â£œâ£±â£¬â ’â¡­â£©â¢†â£½â¢³â¢¯â¡â£®â¢³â¡â£®â¡â €â €â €â €
// â €â£·â¢«â¡Ÿâ¡½â£†â¢â ¥â£“â¢¤â¢‹â¡–â¡¸â¡Œâ ¶â£‰â¢¦â ±â£±â¡‰â¢®â¢±â¡˜â¡†â â£¤â “â£â¢£â â£¢â •â¡ºâ¢½â »â£â¢²â£±â ¾â£­â£›â¢®â£â¢®â¡³â¡½â¡â €â €â €â €â €
// â €â¢¸â£»â£œâ¡³â£â¡»â£”â¢£â â£–â ¸â£±â¢˜â¡£â¢•â¡ªâ •â¢¦â¡™â¢†â¡‡â¢â¡¸â£‰â¢¦â ¹â¡Œâ¢â¢â¡±â¢â¡±â¢â¡±â¡¼â¡¾â£­â£›â¢¶â¡¹â£â¡¼â££â¢Ÿâ¡â €â €â €â €â €â €
// â €â €â¢·â£«â ·â£­â¢³â£â¢·â¢¾â£ˆâ¡“â ¦â£â¡’â §â¡œâ£™â ¦â¡™â¢¦â£¿â¡¦â¢±â¢Šâ¢¦â¢‹â¡¼â£‰â ¦â¡“â£¬â£±â¢¾â¡¹â£â¢·â££â¢Ÿâ£®â¢³â¡â£¾â£±â â €â €â €â €â €â €â €
// â €â €â ˆâ¢¯â£Ÿâ¡¼â£³â¢â¡Ÿâ£®â¢¯â¡½â£³â¢¦â£™â¡œâ¡œâ¡¢â¢â¡˜â¢¦â¡™â¡´â¢‹â¡œâ£¢â¢â¢²â£¡â ¾â£µâ¢«â¡â£§â¢»â¡¼â£¿â£¿â¡¾â£œâ¢§â¡»â£¶â ‹â €â €â €â €â €â €â €â €
// â €â €â €â ˆâ¢¿â¢¾â¡µâ£›â ¾â£µâ£¿â£¾â£­â¢¯â¡â£¾â£¹â¢³â¡Ÿâ£â¢¦â¡³â£œâ¡³â£â¢¶â£«â¢Ÿâ¡¼â£»â£¼â£³â¢»â£¼â££â ¿â£½â£›â¢·â¡¹â£®â Ÿâ â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â »â£½â£¯â£Ÿâ£¿â£¿â¡¿â£â¢¾â¡¹â¢¶â£­â¢³â¡â£®â¢³â¡â£§â¢»â¡œâ£§â£›â¢®â£³â¢³â¢¾â£»â¢Ÿâ£¾â£½â£›â¡¶â£¹â¢®â ¿â ‹â €â €â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â ˆâ »â£¿â£·â£¹â¢â¡½â¢®â£â¡³â£â¢·â¡¹â£â¢·â¡¹â£â¢·â£¿â£§â£Ÿâ¢®â£³â£›â¡¾â£â¡»â£â£½â¢¿â¡½â Ÿâ â €â €â €â €â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â ˆâ ³â¢»â¡¿â£¼â¡³â£â¢·â¡¹â£â¢·â¡¹â£â¢·â¡¹â£¾â£¿â£¿â¢¿â£«â£¿â£¿â¡œâ£§â¢Ÿâ¡¾â œâ ‹â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â ‰â “â ¿â£¹â¡â¡µâ¢¯â¡â£µâ£«â£â£µâ£³â¡â£¼â¢£â¡·â£»â¡¼â ½â šâ ‰â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €
// â €â €â €â €â €â €â €â €â €â €â €â €â €â €â €â ˆâ ‰â ‘â ›â ’â ›â šâ “â “â ›â Šâ ‰â ‰â €â â €â €â €â €â €â €
using {SettingsLib.loadTipSettingsRaw} for IERC725Y;
using {SettingsLib.decodeTipSettings} for bytes;
using {ERC165Checker.supportsInterface} for address;

///
/// @title Contract allowing a ğŸ†™ to react when receiving a new follower, by tipping ğŸ¥” $POTATO tokens to
/// this new follower. Can be used as an automated incentive mechanism.
///
/// @author Jean Cavallera (CJ42)
///
/// @dev Terminology:
/// - BPT = "Before Potato Tipper" = for followers that already followed a user before it connected the Potato Tipper
/// - APT = "After Potato Tipper" = for followers that followed a user after it connected the Potato Tipper"
///
/// @notice âš ï¸ Disclaimer: this contract has not been formally audited by an external third party
/// auditor. The contract does not guarantee to be bug free. Use responsibly at your own risk.
///
contract PotatoTipper is IERC165, ILSP1Delegate, PotatoTipperConfig {
    using Strings for address;

    /// @dev Track `follower` addresses that received a tip already from a `user`
    mapping(address user => mapping(address follower => bool tippedAPT)) internal _tipped;

    /// @dev Track followers that followed a `user` AFTER it connected the Potato Tipper.
    /// Regardless of whether they received a tip (because of LSP7 failed token transfer)
    mapping(address user => mapping(address follower => bool followedAPT)) internal _hasFollowedPostInstall;

    /// @dev Track existing followers BEFORE the user connected the Potato Tipper, to make them not eligible for a tip.
    /// Populated via `_handleOnUnfollow(...)`. Used to keep them ineligible for a tip if they re-follow.
    mapping(address user => mapping(address follower => bool followedBPT)) internal
        _existingFollowerUnfollowedPostInstall;

    /// Read functions
    /// ---------------

    /// @notice This contract only supports the LSP1 Universal Receiver Delegate and ERC165 interfaces.
    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return interfaceId == type(ILSP1Delegate).interfaceId || interfaceId == type(IERC165).interfaceId;
    }

    /// @notice Check if a `follower` has already been tipped by a `user`.
    function hasReceivedTip(address follower, address user) external view returns (bool) {
        return _tipped[user][follower];
    }

    /// @notice Returns if `follower` has followed `user` since it connected its ğŸ†™ to the Potato Tipper contract.
    function hasFollowedPostInstall(address follower, address user) external view returns (bool) {
        return _hasFollowedPostInstall[user][follower];
    }

    /// @notice Returns true if `follower` was already a follower of `user` before it connected the Potato Tipper and
    /// later unfollowed. Defines existing followers that are not eligible for a tip.
    function existingFollowerUnfollowedPostInstall(address follower, address user) external view returns (bool) {
        return _existingFollowerUnfollowedPostInstall[user][follower];
    }

    /// Handler functions
    /// ---------------

    /// @notice Handle follow/unfollow notifications + automatically tip ğŸ¥” tokens to new follower.
    ///
    /// @dev Called by the `universalReceiver(...)` function from a user's ğŸ†™.
    ///
    /// LSP26 notification calls don't revert, but calls from a ğŸ†™ to this function could revert.
    /// Avoid reverting so the internal call stack trace stays clear of revert errors, and return a `messageStatus`
    /// that can be decoded from the `returnedData` parameter of the `UniversalReceiver` event on the user's ğŸ†™.
    ///
    /// @param sender The address that notified the user's UP (MUST be the LSP26 Follower Registry)
    /// @param typeId MUST be a follow or unfollow notification type ID
    /// @param data MUST be the follower address sent by the LSP26 Follower registry when notifying
    /// @return messageStatus A user-friendly message describing how the call was handled
    function universalReceiverDelegate(
        address sender,
        uint256, // value (unused parameter)
        bytes32 typeId,
        bytes calldata data
    )
        external
        returns (bytes memory messageStatus)
    {
        if (sender != address(_FOLLOWER_REGISTRY)) return unicode"â›“ï¸â€ğŸ’¥âŒ Not triggered by the LSP26 Follower Registry";
        if (data.length != 20) return unicode"â›“ï¸â€ğŸ’¥âŒ Invalid data received. Must be a 20 bytes address";

        // Only ğŸ†™âœ… allowed to receive tips, ğŸ”‘âŒ not EOAs
        address follower = address(bytes20(data));
        if (!follower.supportsInterface(_INTERFACEID_LSP0)) return unicode"â›“ï¸â€ğŸ’¥âŒ Only ğŸ†™ allowed to receive tips";

        if (typeId == _TYPEID_LSP26_FOLLOW) return _handleOnFollow(follower);
        if (typeId == _TYPEID_LSP26_UNFOLLOW) return _handleOnUnfollow(follower);

        return unicode"â›“ï¸â€ğŸ’¥âŒ Not a follow/unfollow notification";
    }

    /// @notice Handle a new follower notification and tip ğŸ¥” $POTATO tokens if the follower is eligible.
    ///
    /// @dev Before attempting to transfer a tip, re-validate against the LSP26 Follower Registry to ensure
    /// this function is running in the context of a legitimate follow notification and not from a spoofed call.
    /// Also rejects re-follow attempts from pre-existing followers.
    ///
    /// @return message A human-readable message returned to indicate successful tip, or an error reason.
    function _handleOnFollow(address follower) internal returns (bytes memory message) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(follower, msg.sender);
        if (!isFollowing) return unicode"â›“ï¸â€ğŸ’¥âŒ Not a legitimate follow";

        // Prevent double tipping a follower if unfollow -> re-follow ğŸ¥” ğŸšœ
        if (_tipped[msg.sender][follower]) return unicode"ğŸ”âŒ Follower already tipped";

        // Existing followers are not eligible. A user does not gain any benefit from tipping them if they re-follow
        if (_existingFollowerUnfollowedPostInstall[msg.sender][follower]) {
            return unicode"ğŸ”âŒ Existing followers not eligible to receive tips";
        }

        if (!_hasFollowedPostInstall[msg.sender][follower]) _hasFollowedPostInstall[msg.sender][follower] = true;

        bytes memory settingsValue = IERC725Y(msg.sender).loadTipSettingsRaw();
        (bool decodingSuccess, SettingsLib.TipSettings memory tipSettings, bytes memory decodingError) =
            settingsValue.decodeTipSettings();

        // Decoding also validates the tip settings
        if (!decodingSuccess) return decodingError;

        (bool isEligible, bytes memory eligibilityError) =
            _validateTipEligibilityCriterias(follower, tipSettings.minimumFollowers, tipSettings.minimumPotatoBalance);
        if (!isEligible) return eligibilityError;

        (bool canTransferTip, bytes memory preTransferError) = _validateCanTransferTip(tipSettings.tipAmount);
        if (!canTransferTip) return preTransferError;

        return _transferTip(follower, tipSettings.tipAmount);
    }

    /// @notice Monitor unfollow activity to mark existing followers BPT as ineligible for a tip if they re-follow.
    ///
    /// @dev Re-validate against the LSP26 Follower registry to ensure this function is running in the context of
    /// a legitimate unfollow notification and not from a spoofed call. Existing followers are tracked if we observe
    /// an unfollow without any prior follow registration AFTER the user connected the Potato Tipper (APT).
    ///
    /// @return message A human-readable message to describe the context of the unfollow action.
    function _handleOnUnfollow(address follower) internal returns (bytes memory message) {
        bool isFollowing = _FOLLOWER_REGISTRY.isFollowing(follower, msg.sender);
        if (isFollowing) return unicode"â›“ï¸â€ğŸ’¥âŒ Not a legitimate unfollow";

        if (_tipped[msg.sender][follower]) return unicode"ğŸ‘‹ğŸ» Already tipped, now unfollowing. Goodbye!";

        if (!_hasFollowedPostInstall[msg.sender][follower]) {
            _existingFollowerUnfollowedPostInstall[msg.sender][follower] = true;
            return unicode"ğŸ‘‹ğŸ» Assuming existing follower BPT is unfollowing. Goodbye!";
        }

        return unicode"ğŸ‘‹ğŸ» Sorry to see you go. Hope you re-follow soon! Goodbye!";
    }

    // Internal helpers
    // ----------------

    /// @dev Check if a follower is eligible to receive a tip according to the provided user's tip settings.
    /// Returns an error message explaining why the follower is not eligible.
    function _validateTipEligibilityCriterias(
        address follower,
        uint256 minimumFollowerCountRequired,
        uint256 minimumPotatoBalanceRequired
    ) internal view returns (bool isEligible, bytes memory errorMessage) {
        if (_FOLLOWER_REGISTRY.followerCount(follower) < minimumFollowerCountRequired) {
            return (false, unicode"ğŸ”âŒ Not eligible for tip: minimum follower required not met");
        }

        if (_POTATO_TOKEN.balanceOf(follower) < minimumPotatoBalanceRequired) {
            return (false, unicode"ğŸ”âŒ Not eligible for tip: minimum ğŸ¥” balance required not met");
        }

        return (true, "");
    }

    /// @notice Ensure this contract can transfer `tipAmount` of ğŸ¥” $POTATO tokens on behalf of the user.
    /// Returns an error message explaining why the tip cannot be transferred.
    ///
    /// @dev These checks are also done inside the Potato token contract (LSP7), but performed earlier to:
    /// 1. Avoid making the follower pay the gas cost of a token transfer reverting (external call + return error data).
    /// 2. Return an early error message and pass it to the `returnedData` of the `UniversalReceiver` event.
    function _validateCanTransferTip(uint256 tipAmount)
        internal
        view
        returns (bool canTransferTip, bytes memory errorMessage)
    {
        if (_POTATO_TOKEN.balanceOf(msg.sender) < tipAmount) {
            return (false, unicode"âš™ï¸âš ï¸ Not enough ğŸ¥” left in user's balance");
        }

        if (_POTATO_TOKEN.authorizedAmountFor(address(this), msg.sender) < tipAmount) {
            return (false, unicode"âš™ï¸âš ï¸ Not enough ğŸ¥” left in user's tipping budget");
        }

        return (true, "");
    }

    /// @notice Transfer `tipAmount` of ğŸ¥” $POTATO tokens to the new `follower`.
    ///
    /// @dev Use `try {} catch {}` to transfer the tip to prevent any sub-calls from making the whole call revert.
    /// Returns a success or error message to describe if the tip transfer was successful or not.
    function _transferTip(address follower, uint256 tipAmount) internal returns (bytes memory successOrErrorMessage) {
        _tipped[msg.sender][follower] = true;

        try _POTATO_TOKEN.transfer({
            from: msg.sender, // ğŸ†™ that was â¬…ï¸ followed
            to: follower, // ğŸ†™ that is following â¡ï¸
            amount: tipAmount, // amount of ğŸ¥”ğŸ¥”ğŸ¥” to tip
            force: false, // Default to false, but already checked that follower is a ğŸ†™, so we know it supports LSP1
            data: unicode"Thanks for following! Tipping you some ğŸ¥”" // context for the token transfer
        }) {
            emit PotatoTipSent({from: msg.sender, to: follower, amount: tipAmount});
            return abi.encodePacked(unicode"ğŸ¥”âœ… Successfully sent tip to new follower: ", follower.toHexString());
        } catch (bytes memory errorData) {
            // If the token transfer failed (because the call to the `universalReceiver(...)` function reverted when
            // notifying sender / recipient, or any sub-calls), revert state and mark the follower as not tipped.
            delete _tipped[msg.sender][follower];

            emit PotatoTipFailed({from: msg.sender, to: follower, amount: tipAmount, errorData: errorData});
            return abi.encodePacked(unicode"ğŸ¥”âŒ Failed to send tip to ", follower.toHexString(), ". LSP7 transfer reverted");
        }
    }
}
